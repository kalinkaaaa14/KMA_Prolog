% лектор(кл, прізвище, дата_народження, кп, ставка,кк).
% предмет(кп, назва, тип, програма).
% група(кг, курс, напрям).
% розклад(кл, кп, кг).
% кафедра(кк, найменування).
% посада(кп, назва).

лектор(кл1,піб(гриценко,олег,олегович), дата(11,3,1988), кпс1,10000,кк1).
лектор(кл2,піб(костенко,маргарита,петрівна), дата(15,12,1976), кпс2,7000,кк1).
лектор(кл3,піб(маляренко,олексій,петрович), дата(17,11,1978), кпс1,6700,кк2).
лектор(кл4,піб(хоменко,марія,максимівна), дата(18,10,1976), кпс3,9000,кк2).
лектор(кл5,піб(морко,антон,олегович), дата(10,10,1960), кпс2,8000,кк2).
лектор(кл6,піб(ленко,дарина,павлівна), дата(12,12,1981),кпс3,5000,кк3).

предмет(кп1,математика,"н","бакалаврська").
предмет(кп2,математика,"в","бакалаврська").
предмет(кп3,джава,"н","бакалаврська").
предмет(кп4,дизайн,"н","бакалаврська").

група(кг1,1,"КН").
група(кг2,1,"ІПЗ").
група(кг3,2,"ІПЗ").
група(кг4,2,"ПМ").

розклад(кл1,кп1,кг1).
розклад(кл1,кп1,кг2).
розклад(кл1,кп2,кг1).
розклад(кл2,кп1,кг3).
розклад(кл3,кп2,кг4).
розклад(кл6,кп2,кг3).
розклад(кл5,кп3,кг3).
розклад(кл3,кп3,кг4).

кафедра(кк1,"Кафедра математики").
кафедра(кк2,"Кафедра інформатики").
кафедра(кк3,"Кафедра мультимедійних систем").

посада(кпс1,доцент).
посада(кпс2,викладач).
посада(кпс3,викладач).

%===========================================================================================================================================================================================
% В яких групах (курс, напрям) певного напряму викладається математика? 
% якщо якась група буде слухати кілька предметів, які називаються математика(наприклад, коли 1 математика - нормативна, а 2 - вибіркова),то у результуючій множині будуть повтори,
% тому вживаємо setof
завданняПершеПовтор(Напрям,КодГрупи,Курс,Напрям) :- предмет(Кп,математика,_,_),
                                                    група(КодГрупи,Курс,Напрям),
                                                    розклад(_,Кп,КодГрупи).

% Якби предикат мав вигляд завданняПерше(+Напрям,-Результат), тобто 1 параметр - конкретизований, а 2  - вільна змінна, то запит мав би мати наступний вигляд: завданняПерше("КН",Результат).
% Даний запит повертає всі групи(Код групи, Курс,Напрям) певного напряму(у даному випадку "КН"), які слухають математику. Наприклад: Результат = [(кг1, 1, "КН")].
% Якби предикат мав вигляд завданняПерше(-Напрям, +Результат), тобто коли 1 параметр - вільна змінна, а 2- конкретизований, то запит мав би мати наступний вигляд: 
% завданняПерше(Напрям,[(кг1, 1, "КН")]). Даний запит повертає напрями,групи яких слухають математику(тобто обернений запит до попереднього). Наприклад: Напрям = "КН".
% Якби предикат мав вигляд завданняПерше(-Напрям,-Результат), тобто обидва параметри - вільні змінні, то запит мав би мати наступний вигляд:  завданняПерше(Напрям, Результат).
% Даний запит повертає для кожного можливого значення змінної Напрям список груп, які слухають математику. Наприклад : Напрям = "ІПЗ", Результат = [(кг2, 1, "ІПЗ"),  (кг3, 2, "ІПЗ")] ;
% Напрям = "КН", Результат = [(кг1, 1, "КН")] ; Напрям = "ПМ",Результат = [(кг4, 2, "ПМ")].
% Якби предикат мав вигляд завданняПерше(+Напрям, +Результат), то запит мав би мати наступний вигляд: завданняПерше("КН",[(кг1, 1, "КН")]), який повертає true, так як група з кодом кг1,
% 1 курсу, і напрямом КН слухає математику.
% Отже, завданняПерше(?Напрям, ?Результат).
 
завданняПерше(Напрям,Результат) :- setof((X,Y,Z), завданняПершеПовтор(Напрям,X,Y,Z), Результат).

% =========================================================================================================================================================================================
% Визначити викладача з максимальною ставкою, який викладає певну дисципліну.
викладачіДисципліни(Предмет, Викладачі) :- предмет(Кп,Предмет,_,_),розклад(Викладачі,Кп,_).
всіСтавки(Предмет,Ставки) :- findall(Ставка, (викладачіДисципліни(Предмет,Викладачі), лектор(Викладачі,_,_,_,Ставка,_)),Ставки).

% Якби предикат мав вигляд завданняДруге(+Предмет,-Прізвище,-МаксСтавка), то запит мав би мати наступний вигляд: завданняДруге(математика,Прізвище,Ставка). Даний запит повертає ПІБ викладача,
% який веде математику і має найбільшу ставку серед усіх. Наприклад: Прізвище = піб(гриценко, олег, олегович),Ставка = 10000.
% Якби предикат мав вигляд завданняДруге(+Предмет,+Прізвище, +МаксСтавка), то запит мав би мати наступний вигляд: завданняДруге(математика,  піб(гриценко, олег, олегович),10000), який
% повернув би true, так як викладач математики з найбільшою ставкою - Гриценко Олег Олегович з ставкою 10000, у інакшому випадку - false.
% Якби предикат мав вигляд завданняДруге(-Предмет,+Прізвище,-МаксСтавка) або завданняДруге(-Предмет,-Прізвище,+МаксСтавка) то запит мав би мати наступний вигляд:
% завданняДруге(Предмет,піб(гриценко, олег, олегович),МаксСтавка) або завданняДруге(Предмет,Піб,10000). Перший із них перевіряє, чи існує такий предмет, де максимальну ставку має конкретно
% визначений викладач. Якщо так, то повертає його ставку, якщо даний викладач немає максимальної ставки ні у якому предметі , то повертає false.Другий запити перевіряє, чи є такий предмет
% максимальна ставка якого рівна 10000. Якщо так, то повертає ПІБ викладача, який має дану максимальну ставку, якщо ні - то false.
% Отже,завданняДруге(?Предмет, ?Прізвище, ?МаксСтавка). 
 
завданняДруге(Предмет,Прізвище,МаксСтавка) :- всіСтавки(Предмет,Ставки),
                                              sort(Ставки,ВідсСтавки),
                                              last(ВідсСтавки,МаксСтавка),
                                              лектор(_,Прізвище,_,_,МаксСтавка,_).

%=========================================================================================================================================================================================
% Які викладачі (прізвище, дата народження, посада) читать ті самі предмети(хоча б 1), що і певний викладач?
дисципліниВикладача(Викладач,Дисципліни) :- предмет(Дисципліни,_,_,_),розклад(Викладач,Дисципліни,_).

завданняТретє1Повтори(піб(Прізвище,Імя,Побатькові),Піб,Дата,Посада) :- лектор(КодВикл,піб(Прізвище,Імя,Побатькові),_,_,_,_),
                                                                дисципліниВикладача(КодВикл,Дисципліни),
                                                                лектор(Кл,Піб,Дата,Кпс,_,_),посада(Кпс,Посада), 
                                                                Кл \= КодВикл,розклад(Кл,Дисципліни,_).

% Якби предикат мав вигляд завданняТретє1(+Піб, -Результат), то запит мав би такий вигляд: завданняТретє1(піб(гриценко,олег,олегович),Результат). Даний запит знаходив би усіх лекторів, які 
% мають хоча б 1 спільний предмет із викладачем, ПІБ якого задавався 1 параметром. Результат = [(піб(костенко, маргарита, петрівна), дата(15, 12, 1976), викладач),
% (піб(ленко, дарина, павлівна), дата(12, 12, 1981), викладач),  (піб(маляренко, олексій, петрович), дата(17, 11, 1978), доцент)].
% Якби предикат мав вигляд завданняТретє1(+Піб, +Результат), то запит мав би такий вигляд: завданняТретє1(піб(гриценко,олег,олегович),[(піб(костенко, маргарита, петрівна), дата(15, 12, 1976), викладач),
% (піб(ленко, дарина, павлівна), дата(12, 12, 1981), викладач),  (піб(маляренко, олексій, петрович), дата(17, 11, 1978), доцент)]), який повернув б true, адже ліст викладачів і справді мають 
% хоча б 1 предмет з Гриценком. Якщо забрати якийсь елемент із ліста, або з надати неправильні значення 2 аргументом, то запит повернув би false.
% Якби предикат мав вигляд завданняТретє1(-Піб,+Результат), то запит мав би такий вигляд: завданняТретє1(Піб, [(піб(костенко, маргарита, петрівна), дата(15, 12, 1976), викладач),
% (піб(ленко, дарина, павлівна), дата(12, 12, 1981), викладач),  (піб(маляренко, олексій, петрович), дата(17, 11, 1978), доцент)]). Наприклад: Піб = піб(гриценко, олег, олегович). Даний запит
% знайшов всіх викладачів, які мають хоча б 1 спільний предмет з списком усіх викладачів, які передавались 2 аргументом).
% Якби предикат мав вигляд завданняТретє1(-Піб,-Результат), то запит мав би такий вигляд: завданняТретє1(Піб,Результат). Даний результат видає для кожного значення Піб - власний
% список Результат, який формується для кожного значення Піб окремо.
% Отже, завданняТретє1(?Піб, ?Результат).
завданняТретє1(піб(Прізвище,Імя,Побатькові),Результат) :- setof((X,Y,Z),завданняТретє1Повтори(піб(Прізвище,Імя,Побатькові),X,Y,Z),Результат).


% ==========================================================================================================================================================================================
% Які викладачі (прізвище, кафедра) читать усі ті предмети(усі, можуть ще якісь інші), що і певний викладач?
поганіЛектори(Кодл,Лектори) :- розклад(Кодл,Предмети,_),лектор(Лектори,_,_,_,_,_), not(розклад(Лектори,Предмети,_)).

% Якби предикат мав вигляд завданняТретє2(+Піб, -Піб,-Кафедра), то запит мав би вигляд: завданняТретє2(піб(гриценко,олег,олегович),Піб,Кафедра). Даний запит повертає ПІБ і кафедри 
% таких викладачів, які читають усі ті предмети(і можуть ще якісь інші), як і Гриценко Олег Олегович.
% Якби предикат мав вигляд завданняТретє2(-Піб,+Піб,-Кафедра), то запит мав би вигляд: завданняТретє2(Піб, піб(гриценко, олег, олегович),Кафедра). Даний запит для кожного значення вільної
% змінної ПІБ перевіряє з якими Лекторами Піб Гриценко Олег Олегович веде ті самі предмети і для кожного значення виводить інший результат.
% Якби предикат мав вигляд завданняТретє2(-Піб,-Піб,+Кафедра), то запит мав би такий вигляд: завданняТретє2(Піб,Піб1,"Кафедра математики").Даний запит для кожного значення вільної змінної
% Піб шукає значення викладачів Піб1, перевіряє чи даний викладач із кафедри математики, і якщо так, то виводить цю парю значень у результуючу множину.
% Якби предикат мав вигляд завданняТретє2(-Піб,-Піб1,-Кафедра), то запит мав би такий вигляд: завданняТретє2(Піб,Піб1,Кафедра). Даний запит для кожного значення вільної змінної Піб
% шукає викладачів, які читають усі ті самі предмети, і виводить кожного з них, з кожної кафедри.
% Якби предикт мав вигляд завданняТретє2(+Піб,+Піб1,+Кафедра), то запит мав би такий вигляд: завданняТретє2(піб(гриценко,олег,олегович),піб(костенко, маргарита, петрівна), 
% "Кафедра математики")).Даний запит переевіряє, чи насправді викладач з певної конкретної кафедри, що розміщений 2 аргументом, читає усі предмети викладача, що розміщений під 1 аргументом.
% Якщо це дійсно так - повертає true.
% Отже, завданняТретє2(?Піб,?Піб1,?Кафедра)
завданняТретє2(піб(Прізвище,Імя,Побатькові),Піб,Кафедра) :- лектор(Кодл, піб(Прізвище,Імя,Побатькові),_,_,_,_),
                                                            лектор(Лектори, Піб,_,_,_,КодКаф), кафедра(КодКаф,Кафедра),
                                                            not(поганіЛектори(Кодл,Лектори)), Кодл \= Лектори.
